plugins {
    id 'minecord.conventions'
    id 'jacoco'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
}

repositories {
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

testSets {
    integrationTest
}

dependencies {
    implementation project(":shared")
    // Top.gg bot site API wrapper
    implementation 'com.github.DiscordBotList:Java-Wrapper:v1.0'
    // Caching
    api 'com.github.ben-manes.caffeine:caffeine:2.9.3'
    // Circuit breaker failsafe
    implementation 'dev.failsafe:failsafe:3.2.3'
    // JSON library
    implementation 'org.json:json:20220320'
    // SQLite driver
    implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
    // Used for MCServerPing
    implementation 'dnsjava:dnsjava:3.5.0'
    implementation 'net.kyori:text-serializer-gson:3.0.4'
    // Testing
    integrationTestImplementation sourceSets.test.output
}

tasks.withType(Test) {
    useJUnitPlatform {
        // Uncomment the line below to exclude tags from testing such as "MojangAPI"
        // excludeTags 'MojangAPI'
    }
}

jacoco {
    toolVersion = "0.8.7"
}

tasks.withType(JacocoReport) {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
test.finalizedBy jacocoTestReport
integrationTest.finalizedBy jacocoIntegrationTestReport
jacocoIntegrationTestReport.mustRunAfter jacocoTestReport

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching("**/build.properties") {
        filter ReplaceTokens, tokens: [
                "version": project.version,
                "jdaVersion": jdaVersion
        ]
    }
}
processResources.dependsOn cleanProcessResources
