plugins {
    id 'minecord.conventions-build'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
}

repositories {
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
}

testSets {
    integrationTest
}

dependencies {
    implementation project(":minecord-common")
    implementation project(":mcserverping")
    // Top.gg bot site API wrapper
    implementation 'com.github.DiscordBotList:Java-Wrapper:v1.0'
    // Caching
    api 'com.github.ben-manes.caffeine:caffeine:2.9.3'
    // Circuit breaker failsafe
    implementation 'dev.failsafe:failsafe:3.2.3'
    // JSON library
    implementation 'org.json:json:20220320'
    // SQLite driver
    implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
    // Testing
    integrationTestImplementation sourceSets.test.output
}

tasks.withType(Test) {
    useJUnitPlatform {
        // Uncomment the line below to exclude tags from testing such as "MojangAPI"
        // excludeTags 'MojangAPI'
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
integrationTest.finalizedBy jacocoIntegrationTestReport
jacocoIntegrationTestReport.mustRunAfter jacocoTestReport
